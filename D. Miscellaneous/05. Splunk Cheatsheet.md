# Splunk Cheatsheet

## Search

### Using wildcards

```splunk
eventtype="micros_dt-api-filestore" (application m.t=application micros_container!=platform-*) env=prod*
```

### Field is defined

```splunk
eventtype="micros_dt-api-filestore" (application m.t=application micros_container!=platform-*) (prod* env=prod*) dt_http_status
```

### Field is in a range

```splunk
eventtype="micros_dt-api-filestore" (application m.t=application micros_container!=platform-*) (prod* env=prod*) (dt_http_status >= 400 AND dt_http_status < 500)
```

### Search command

```splunk
eventtype="micros_analytics-service" m.t=application env="prod*" ext.event.platform="web" ext.event.type="operational" ext.event.actionSubject="mediaCardRender"
ext.event.action="failed" ext.event.attributes.failReason="media-client-error"
| search ext.event.attributes.error="*jwt expired*"
```

### Where command with `searchmatch` (using boolean expr)

```splunk
eventtype="micros_analytics-service" m.t=application env="prod*" ext.event.platform="web" ext.event.type="operational" ext.event.actionSubject="mediaCardRender"
ext.event.action="failed" ext.event.attributes.failReason="media-client-error"
| where searchmatch("ext.event.attributes.error=\"*jwt expired*\"")
```

## Manipulating

### Declaring a field using `eval`

```splunk
eventtype="micros_dt-api-filestore" (application m.t=application micros_container!=platform-*) (prod* env=prod*)
| eval instance='ec2.az'." ".'ec2.ip'
```

### Extracting a field using `rex`

```splunk
eventtype="micros_dt-api-filestore" (application m.t=application micros_container!=platform-*) (prod* env=prod*) message="*added to*"
| rex field=message "^.*?added to (?<className>.*?)\..*?$"
| stats count by className
```

### Extracting a field using `rex` (sed method)

```splunk
eventtype="micros_dt-api-filestore" (application m.t=application micros_container!=platform-*) (prod* env=prod*) dt_type="http_access"
| rex field=dt_http_path mode=sed "s/[A-Za-z0-9]{8}\-[A-Za-z0-9]{4}-[A-Za-z0-9]{4}-[A-Za-z0-9]{4}-[A-Za-z0-9]{12}//g"
```

### Coalescing across multiple field values

```splunk
eventtype="micros_dt-api-filestore" (application m.t=application micros_container!=platform-*) (prod* env=prod*)
| eval requestMethod=coalesce('dt_http_method','birdyReq.method','trace.binary_annotations.http_method')
| fields + requestMethod
```

### Adding custom field to results

```splunk
eventtype="micros_dt-api-filestore" (application m.t=application micros_container!=platform-*) (prod* env=prod*)
| eval instance='ec2.az'." ".'ec2.ip'
| fields + instance
```

## Aggregating

### Computing p50, p90, p99 for a value

```splunk
eventtype="micros_dt-api-filestore" (application m.t=application micros_container!=platform-*) (prod* env=prod*)
| search dt_type="http_access" AND dt_http_normalised_url="/file/:uuid/binary"
| stats p50(dt_http_response_time), p90(dt_http_response_time), p99(dt_http_response_time)
```

### Computing bins for a value

```splunk
eventtype="micros_dt-api-filestore" (application m.t=application micros_container!=platform-*) (prod* env=prod*)
| search dt_type="http_access" AND dt_http_normalised_url="/file/:uuid/binary"
| bin _time span=10s
| stats p50(dt_http_response_time), p90(dt_http_response_time), p99(dt_http_response_time) by _time
```

## Visualizing

### Variation a statistical result over time

```splunk
eventtype="micros_dt-api-filestore" (application m.t=application micros_container!=platform-*) (prod* env=prod*) dt_type="http_access"
| timechart count by dt_http_normalised_url
```

### Variation of a value over time (default granularity)

```splunk
eventtype="micros_dt-api-filestore" (application m.t=application micros_container!=platform-*) (prod* env=prod*)
| search dt_type="http_access" AND dt_http_normalised_url="/file/:uuid/binary"
| timechart dt_http_response_time
```

### Variation of a value over time (1s granularity)

```splunk
eventtype="micros_dt-api-filestore" (application m.t=application micros_container!=platform-*) (prod* env=prod*)
| search dt_type="http_access" AND dt_http_normalised_url="/file/:uuid/binary"
| timechart span=1s dt_http_response_time
```

### Variation of p50, p90, p99 over time (1s granularity)

```splunk
eventtype="micros_dt-api-filestore" (application m.t=application micros_container!=platform-*) (prod* env=prod*)
| search dt_type="http_access" AND dt_http_normalised_url="/file/:uuid/binary"
| timechart span=1s p50(dt_http_response_time), p90(dt_http_response_time), p99(dt_http_response_time)
```

### Variation of p50, p90, p99 over time (custom bins)

```splunk
eventtype="micros_dt-api-filestore" (application m.t=application micros_container!=platform-*) (prod* env=prod*)
| search dt_type="http_access" AND dt_http_normalised_url="/file/:uuid/binary"
| bin _time span=10s
| timechart p50(dt_http_response_time), p90(dt_http_response_time), p99(dt_http_response_time)
```

## Streaming

### Counting a value by a partition, computing reliability

```splunk
eventtype="micros_dt-api-filestore" (application m.t=application micros_container!=platform-*) (prod* env=prod*) dt_type="http_access"
| eval success_counter=if(dt_http_status == 200, 1, 0)
| streamstats sum(success_counter) as total_success by dt_http_normalised_url
| streamstats count as total_count by dt_http_normalised_url
| stats max(total_success) as successes, max(total_count) as total, max(eval(round((max(total_success)/(max(total_count)))*100, 10))) as reliability by dt_http_normalised_url
```

### Counting a value by a partition, subfiltering

```splunk
eventtype="micros_analytics-service" m.t=application env="prod*"
ext.event.platform="web" ext.event.type="operational" ext.event.actionSubject="mediaCardRender"
ext.event.action="failed" ext.event.attributes.failReason="media-client-error"
| eval jwt_expired=if(searchmatch("ext.event.attributes.error="jwt expired""), 1, 0)
| streamstats sum(jwt_expired) as amount_jwt_expired by ext.event.product
| streamstats count as total_amount by ext.event.product
| stats max(amount_jwt_expired) as total_jwt_expired, max(total_amount) as total_count, avg(eval(round(max(amount_jwt_expired)/max(total_amount)*100, 2))) as ratio  by ext.event.product
```

# Splunk Cheatsheet

## Search

### Using wildcards

```splunk
`micros_my-service` micros_container!=platform-* env=prod*
```

### Field is defined

```splunk
`micros_my-service` example_of_field
```

### Field is in a range

```splunk
`micros_my-http-service` (http_status >= 400 AND http_status < 500)
```

### Search command

```splunk
`micros_my-analytics-service` event_type="operational" subject="mediaCardRender" action="failed"
| search ext.event.attributes.error="*jwt expired*"
```

### Where command with `searchmatch` (using boolean expr)

```splunk
`micros_my-analytics-service` event_type="operational" subject="mediaCardRender" action="failed"
| where searchmatch("ext.event.attributes.error=\"*jwt expired*\"")
```

### Stats on union queries

```splunk
`micros_my-service`
| rename tr.traceId as traceId
| spath "err.message" 
| search "err.code"="FCT_VALIDATION_TOKEN_EXPIRED" 
| join left=L right=R where L.traceId = R.traceId
[ search `micros_my-other-service` msg="Checking permissions using poco policies to create a new FCT."
| rename tr.traceId as traceId]
| stats count by something
```

## Manipulating

### Declaring a field using `eval`

```splunk
`micros_my-service`
| eval instance='ec2.az'." ".'ec2.ip'
```

### Extracting a condition using `eval` and `searchmatch`

```splunk
`micros_my-analytics-service` subject="mediaCardRender" action="succeeded"
| eval isExternalImage=if(searchmatch("ext.event.attributes.fileAttributes.fileId=\"external-image\""), 1, 0)| search isExternalImage=1
```

### Extracting a field using `rex`

```splunk
`micros_my-service` message="*added to*"
| rex field=message "^.*?added to (?<className>.*?)\..*?$"
| stats count by className
```

### Extracting Apache access.log using `rex`

```splunk
`micros_my-service`
| rex field=message (?<src>\d+\.\d+\.\d+\.\d+).+\]\s\"(?<http_method>\w+)\s(?<uri_path>.+)\"\s(?<status>\d+)
| stats count by status
```

### Extracting a field using `rex` (sed method)

```splunk
`micros_my-http-service` dt_type="http_access"
| rex field=dt_http_path mode=sed "s/[A-Za-z0-9]{8}\-[A-Za-z0-9]{4}-[A-Za-z0-9]{4}-[A-Za-z0-9]{4}-[A-Za-z0-9]{12}//g"
```

### Coalescing across multiple field values

```splunk
`micros_my-service` 
| eval requestMethod=coalesce('dt_http_method','birdyReq.method','trace.binary_annotations.http_method')
| fields + requestMethod
```

### Adding custom field to results

```splunk
`micros_my-service` 
| eval instance='ec2.az'." ".'ec2.ip'
| fields + instance
```

## Aggregating

### Computing p50, p90, p99 for a value

```splunk
`micros_my-http-service` 
| search dt_type="http_access" AND dt_http_normalised_url="/file/:uuid/binary"
| stats p50(dt_http_response_time), p90(dt_http_response_time), p99(dt_http_response_time)
```

### Computing bins for a value

```splunk
`micros_my-http-service`
| search dt_type="http_access" AND dt_http_normalised_url="/file/:uuid/binary"
| bin _time span=10s
| stats p50(dt_http_response_time), p90(dt_http_response_time), p99(dt_http_response_time) by _time
```

## Visualizing

### Count over time 

```splunk
`micros_my-service` message="[Limited to 100 per PT1M] Error: Response ended with an error message" | timechart span=3h count
```

### Variation a statistical result over time

```splunk
`micros_my-http-service` dt_type="http_access"
| timechart count by dt_http_normalised_url
```

### Variation of a value over time (default granularity)

```splunk
`micros_my-http-service`
| search dt_type="http_access" AND dt_http_normalised_url="/file/:uuid/binary"
| timechart dt_http_response_time
```

### Variation of a value over time (1s granularity)

```splunk
`micros_my-http-service` 
| search dt_type="http_access" AND dt_http_normalised_url="/file/:uuid/binary"
| timechart span=1s dt_http_response_time
```

### Variation of p50, p90, p99 over time (1s granularity)

```splunk
`micros_my-http-service` 
| search dt_type="http_access" AND dt_http_normalised_url="/file/:uuid/binary"
| timechart span=1s p50(dt_http_response_time), p90(dt_http_response_time), p99(dt_http_response_time)
```

### Variation of p50, p90, p99 over time (custom bins)

```splunk
`micros_my-http-service` 
| search dt_type="http_access" AND dt_http_normalised_url="/file/:uuid/binary"
| bin _time span=10s
| timechart p50(dt_http_response_time), p90(dt_http_response_time), p99(dt_http_response_time)
```

## Streaming

### Counting a value by a partition, computing reliability

```splunk
`micros_my-http-service` dt_type="http_access"
| eval success_counter=if(dt_http_status == 200, 1, 0)
| streamstats sum(success_counter) as total_success by dt_http_normalised_url
| streamstats count as total_count by dt_http_normalised_url
```

### Counting a value by a partition, subfiltering

```splunk
`micros_my-analytics-service` subject="mediaCardRender" action="failed"
| eval jwt_expired=if(searchmatch("ext.event.attributes.error="jwt expired""), 1, 0)
| streamstats sum(jwt_expired) as amount_jwt_expired by ext.event.product
| streamstats count as total_amount by ext.event.product
| stats max(amount_jwt_expired) as total_jwt_expired, max(total_amount) as total_count, avg(eval(round(max(amount_jwt_expired)/max(total_amount)*100, 2))) as ratio  by ext.event.product
```

### Extracting, counting values by partition, computing percentages

```splunk
`micros_my-analytics-service` subject="upload" action="success"
   ext.event.attributes.fileAttributes.fileMimetype="video/*"
   ext.event.attributes.fileAttributes.fileSize=*
   | eval up_to_100mb=if('ext.event.attributes.fileAttributes.fileSize' <= 100000000, 1, 0)
   | eval up_to_500mb=if('ext.event.attributes.fileAttributes.fileSize' > 100000000 AND 'ext.event.attributes.fileAttributes.fileSize' <= 500000000, 1, 0)
   | streamstats sum(up_to_100mb) as amount_up_to_100mb
   | streamstats sum(up_to_500mb) as amount_up_to_500mb
   | streamstats count as total_amount
   | stats avg(eval(round(max(amount_up_to_100mb)/max(total_amount)*100, 2))) as percent_up_to_100mb, 
      avg(eval(round(max(amount_up_to_500mb)/max(total_amount)*100, 2))) as percent_up_to_500mb
```
